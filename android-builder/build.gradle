buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'

        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'org.codehaus.groovy:gradle-groovy-android-plugin:0.3.6'
    }
}

apply plugin: 'maven'

// use com.android.library instead of com.android.application for libraries
apply plugin: 'com.android.library'
apply plugin: 'groovyx.grooid.groovy-android'

android {
    compileSdkVersion 19
    buildToolsVersion '22.0.1'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'org.codehaus.groovy:groovy:2.4.3:grooid'
    compile 'com.android.support:support-v4:19.0.1'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

// Deployment
apply plugin: 'com.jfrog.bintray'

// set bintrayUser & bintrayKey in gradle.properties
bintray {
    user = properties.bintrayUser ?: "fake_user"
    key = properties.bintrayKey ?: "fake_key"
    publications = ['groovyMaven']

    pkg {
        repo = 'grooid-tools' // or your repo name
        name = project.name // somehow project.* doesn't work in this closure
        desc = project.description
        websiteUrl = 'https://github.com/karfunkel/grooid-tools'
        issueTrackerUrl = 'https://github.com/karfunkel/grooid-tools/issues'
        licenses = ['Apache-2.0']
        labels = ['Groovy', 'Android', 'Builder']
    }
    dryRun = true // whether to run this as dry-run, without deploying
}
